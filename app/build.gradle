import com.android.ddmlib.DdmPreferences

apply plugin: 'jacoco'
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.cookpad.android.licensetools'
apply plugin: 'com.github.ben-manes.versions'

// Manifest version
def versionMajor = 1
def versionMinor = 1
def versionPatch = 0

// CI
def isCi = "true".equals(System.getenv("CI"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

def packageName = "io.github.droidkaigi.confsched2017"

// for debug info
def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitBranch = (System.getenv("CIRCLE_BRANCH") != null) ? System.getenv("CIRCLE_BRANCH") :
        'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion compile_sdk_version as int
    buildToolsVersion build_tools_version
    dataBinding {
        enabled = true
    }

    dexOptions {
        // Skip pre-dexing when running on CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isCi
    }

    defaultConfig {
        applicationId packageName
        minSdkVersion min_sdk_version as int
        targetSdkVersion target_sdk_version as int
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "CONFERENCE_TIMEZONE", "\"Asia/Tokyo\""
        buildConfigField "String", "API_ROOT", "\"https://droidkaigi.github.io/2017/\""
        buildConfigField "String", "STATIC_ROOT", "\"https://droidkaigi.github.io/2017\""
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "GIT_BRANCH", "\"${gitBranch}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        // Referred to https://github.com/circleci/EspressoSample to make the Espresso tests work on CircleCI.
        DdmPreferences.setTimeOut(60000)

        vectorDrawables.useSupportLibrary = true

        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/rxjava.properties'
    }

    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            storeFile rootProject.file("release.keystore")
            storePassword System.getenv("RELEASE_STORE_PASSWORD")
            keyAlias System.getenv("RELEASE_KEY_ALIAS")
            keyPassword System.getenv("RELEASE_KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
            debuggable true
            zipAlignEnabled true
            testCoverageEnabled true
            multiDexEnabled true
        }
        release {
            signingConfig signingConfigs.release
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
        }
    }

    flavorDimensions "core"
    productFlavors {
        develop {
            dimension "core"
            applicationId "${packageName}.develop"
        }
        production {
            dimension "core"
            applicationId packageName
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
        textReport true
        textOutput 'stdout'
        lintConfig file("lint.xml")
    }

    aaptOptions {
        noCompress 'apk'
    }

    testOptions {
        unitTests.all {
            // configure the set of classes for JUnit tests
            include project.hasProperty("testFilter") ? "**/*${project.ext.testFilter}*Test.class" : '**/*Test.class'
            exclude '**/espresso/**/*.class'

            // configure max heap size of the test JVM
            maxHeapSize = '2048m'

            // configure the test JVM arguments
            jvmArgs '-XX:MaxPermSize=512m', '-XX:-UseSplitVerifier'

            // Specify max number of processes (default is 1)
            maxParallelForks = 1

            // Specify max number of test classes to execute in a test process
            // before restarting the process (default is unlimited)
            forkEvery = 150

            // configure whether failing tests should fail the build
            ignoreFailures false

            systemProperty 'robolectric.logging.enabled', 'true'

            testLogging {
                events 'passed', 'skipped', 'failed', 'standardError'
            }
            jacoco {
                includeNoLocationClasses = true
            }
        }
        // for Robolectric
        unitTests.includeAndroidResources = true
    }
}

configurations {
    all {
        resolutionStrategy {
            force "com.android.support:support-annotations:${support_lib_version}"
            force "com.android.support:multidex:${multidex_version}"
        }
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:${kotlin_version}"

    // Support Library
    implementation "com.android.support:support-v4:${support_lib_version}"
    implementation "com.android.support:appcompat-v7:${support_lib_version}"
    implementation "com.android.support:support-annotations:${support_lib_version}"
    implementation "com.android.support:design:${support_lib_version}"
    implementation "com.android.support:recyclerview-v7:${support_lib_version}"
    implementation "com.android.support:cardview-v7:${support_lib_version}"
    implementation "com.android.support:customtabs:${support_lib_version}"
    implementation "com.android.support:support-vector-drawable:${support_lib_version}"
    implementation "com.android.support:animated-vector-drawable:${support_lib_version}"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    debugImplementation "com.android.support:multidex:${multidex_version}"

    // Data Binding
    kapt 'com.android.databinding:compiler:3.0.1'

    // Firebase(analytics, crash report)
    implementation 'com.google.firebase:firebase-core:11.6.0'
    implementation 'com.google.firebase:firebase-crash:11.6.0'

    // Network
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.okhttp3:okhttp:3.9.0'

    // Structure
    implementation 'com.google.dagger:dagger:2.13'
    kapt 'com.google.dagger:dagger-compiler:2.13'
    api 'javax.annotation:jsr250-api:1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.6'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'uk.co.chrisjenx:calligraphy:2.3.0'

    // Android Utility
    implementation 'com.deploygate:sdk:3.1.1'
    implementation 'com.jakewharton.timber:timber:4.6.0'
    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    debugImplementation 'com.facebook.stetho:stetho-timber:1.5.0@aar'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    kapt 'com.github.gfx.android.orma:orma-processor:4.2.5'
    implementation 'com.github.gfx.android.orma:orma:4.2.5'
    implementation 'com.hannesdorfmann.fragmentargs:annotation:3.0.2'
    kapt 'com.hannesdorfmann.fragmentargs:processor:3.0.2'
    implementation 'com.chibatching.kotpref:kotpref:2.2.0'

    // UI
    implementation 'org.lucasr.twowayview:core:1.0.0-SNAPSHOT@aar'
    implementation 'org.lucasr.twowayview:layouts:1.0.0-SNAPSHOT@aar'
    implementation 'com.google.android:flexbox:0.3.1'
    implementation 'net.opacapp:multiline-collapsingtoolbar:1.6.0'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'

    // Debug
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    debugImplementation 'com.tomoima.debot:debot:2.0.3'
    releaseImplementation 'com.tomoima.debot:debot-no-op:2.0.3'

    // fps, track the frame rate of animation view.
    debugImplementation 'jp.wasabeef:takt:1.0.5'

    // Test
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.5.1'
    testImplementation 'com.squareup.assertj:assertj-android:1.2.0'
    testImplementation 'org.mockito:mockito-core:2.11.0'
    androidTestImplementation 'org.mockito:mockito-android:2.11.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation 'com.squareup.assertj:assertj-android:1.2.0'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.1'
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:${kotlin_version}"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:${kotlin_version}"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:${kotlin_version}"
    testImplementation 'com.taroid.knit:knit:0.1.2'
    testImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.5.0'
}

jacoco {
    toolVersion = "0.7.9"
}

kapt {
    correctErrorTypes = true
}

// A list of directories which should be included in coverage report
def coverageSourceDirs = ['src/main/java']
// A list of files which should be excluded from coverage report since they are generated and/or framework code
def coverageExcludeFiles = ['**/R.class', '**/R$*.class', '**/DataBindingInfo.class', '**/android/databinding/*.*', '**/com/android/**/*.*', '**/BR.class']

task jacocoTestReportProduction(type: JacocoReport, dependsOn: ['testProductionDebugUnitTest']) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
        xml.destination "${buildDir}/reports/jacoco/test/jacocoTestReportProd.xml"
        html.destination "${buildDir}/reports/coverage/prod"
        classDirectories = files(
                fileTree(
                        dir: "${buildDir}/intermediates/classes/production/debug",
                        exclude: coverageExcludeFiles))
    }
    sourceDirectories = files(coverageSourceDirs)
    executionData = files "${buildDir}/jacoco/testProductionDebugUnitTest.exec"

    doLast {
        println "jacoco xml report has been generated to file://${buildDir}/reports/jacoco/test/jacocoTestReportProd.xml"
        println "jacoco html report has been generated to file://${reports.html.destination}/index.html"
    }
}

task jacocoTestReportDevelop(type: JacocoReport, dependsOn: ['testDevelopDebugUnitTest']) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
        xml.destination "${buildDir}/reports/jacoco/test/jacocoTestReportDevelop.xml"
        html.destination "${buildDir}/reports/coverage/develop"
        classDirectories = files(
                fileTree(
                        dir: "${buildDir}/intermediates/classes/develop/debug",
                        exclude: coverageExcludeFiles))
    }
    sourceDirectories = files(coverageSourceDirs)
    executionData = files "${buildDir}/jacoco/testDevelopDebugUnitTest.exec"

    doLast {
        println "jacoco xml report has been generated to file://${buildDir}/reports/jacoco/test/jacocoTestReportDevelop.xml"
        println "jacoco html report has been generated to file://${reports.html.destination}/index.html"
    }
}

tasks['check'].dependsOn checkLicenses

// report only stable versions
dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

// MUST BE AT THE BOTTOM
apply plugin: 'com.google.gms.google-services'
